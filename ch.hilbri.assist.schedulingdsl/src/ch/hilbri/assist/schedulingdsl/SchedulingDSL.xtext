grammar ch.hilbri.assist.schedulingdsl.SchedulingDSL with org.eclipse.xtext.common.Terminals

import "ch.hilbri.assist.datamodel.model"

AssistModel: 
	'Global' 	'{' 
					'System name' '=' systemName=STRING ';' 
	'}'
	
	'Hardware' 	'{'	
					(hardwareContainer+=HardwareElementContainer)+
	'}'

	'Software' '{'
					(applications+=Application)+ 
	'}'

	('Relations' '{'
					(schedulingRelations+=SchedulingRelation)*
	'}')?
;

/* ============= HARDWARE ================================== */

HardwareElementContainer: 
	Compartment | Box | Board
;

Compartment: 
	'Compartment' name=ID '{'
		(boxes+=Box)+
	'}'
;

Box:
	'Box' name=ID '{'
		(boards+=Board)+
	'}'
;
	
Board:
	'Board' name=ID '{'
		(processors+=Processor)+
//		(ioAdapters+=IOAdapter)*
	'}'
;

Processor:
	'Processor' name=ID '{'
		(cores+=Core)+
	'}'
;

Core:
	'Core' name=ID '{' 
	'}'
;

//IOAdapter:
//	'I/O adapter' '{'
//		'sharedUnits' '=' sharedCount=INT ';' 	
//	'}' 
//;

/* ============= SOFTWARE ================================== */

Application:
	'Application' name=ID '{'
		(threads+=Thread)+
//		(ioAdapterRequirements+=IOAdapterRequirement)
	'}'
;

Thread:
	'Thread' '{'
		'Duration' 			'=' 	duration=INT 			';'
		'Location'			'='		core=[Core]				';'
		'Period'			'='		period=INT				';'
		('JitterEarly'		'='		earlyTolerance=INT		';')?
		('JitterLate'		'='		lateTolerance=INT		';')?
		('LatestFirstStart'	'='		latestFirstStart=INT	';')?
		('LatestFirstEnd'	'='		latestFirstEnd=INT		';')?
		('Initialization'	'='		initTime=INT			';')?
		('MaxSlices'		'='		maxSlices=INT			';')?
		('MinSliceDuration'	'='		minSliceDuration=INT	';')?
	'}'
;

/* ============= RELATIONEN ================================== */
enum ReferencePointType:
	START 	= 'Start'	|
	END		= 'End'
;

SchedulingRelation:
	threadBefore=[Thread] '.' refBefore=ReferencePointType 'before'
	threadAfter=[Thread]  '.' refAfter=ReferencePointType
	('with delay' delay=INT)? ';' 
;